//------------------------------------------------------------------------
// 简称: LinearRegArray
// 名称: 求数组的线性回归
// 类别: 用户函数
// 类型: 内建函数
// 输出: 布尔型
//------------------------------------------------------------------------

PARAMS
	NumericArrayRef arr;
	Numeric TgtPos(0);
	NumericRef LRSlope;
	NumericRef LRAngle;
	NumericRef LRIntercept;
	NumericRef LRValue;
VARS
	Numeric arrSize;
	Numeric SumXY(0);
	Numeric SumY;
	Numeric SumX;
	Numeric SumXSqr;
	Numeric Divisor;
	Numeric i;
BEGIN
	arrSize = GetNumericArraySize(arr);
	if (arrSize > 1) {
		SumX = arrSize * (arrSize - 1) * 1 / 2;
		SumXSqr = arrSize * (arrSize - 1) * (2 * arrSize - 1) * 1 / 6;
		Divisor = Sqr(SumX) - arrSize * SumXSqr;
		SumY = SummationArray(arr);
		for i = 0 to arrSize - 1 {
			SumXY = SumXY + i * arr[i];
		}

		LRSlope = (arrSize * SumXY - SumX * SumY) / Divisor;
		LRAngle = Atan(LRSlope);
		LRIntercept = (SumY - LRSlope * SumX) / arrSize;
		LRValue = LRIntercept + (arrSize - 1 - TgtPos) * LRSlope;
		RETURN true;
	}
	else {
		RETURN false;
	}
END

//------------------------------------------------------------------------
// 编译版本	GS2014.10.25
// 版权所有	TradeBlazer Software 2003－2014
// 更改声明	TradeBlazer Software保留对TradeBlazer平
//			台每一版本的TradeBlazer公式修改和重写的权利
//------------------------------------------------------------------------
