//------------------------------------------------------------------------
// 简称: Extremes
// 名称: 求极值
// 类别: 用户函数
// 类型: 内建函数
// 输出: 数值型
//------------------------------------------------------------------------

PARAMS
	NumericSeries Price(1);
	Numeric Length(10);
	Bool bMax(true);
	NumericRef ExtremeBar;
VARS
	NumericSeries MyVal;
	NumericSeries MyBar;
	Numeric i;
BEGIN
	MyVal = Price;
	MyBar = 0;

	if (CurrentBar <= Length - 1 || MyBar[1] == Length - 1) {
		for i = 1 to Length - 1 {
			if (bMax) {
				if (Price[i] > MyVal) {
					MyVal = Price[i];
					MyBar = i;
				}
			}
			else {
				if (Price[i] < MyVal) {
					MyVal = Price[i];
					MyBar = i;
				}
			}
		}
	}
	else {
		if (bMax) {
			if (Price >= MyVal[1]) {
				MyVal = Price;
				MyBar = 0;
			}
			else {
				MyVal = MyVal[1];
				MyBar = MyBar[1] + 1;
			}
		}
		else {
			if (Price <= MyVal[1]) {
				MyVal = Price;
				MyBar = 0;
			}
			else {
				MyVal = MyVal[1];
				MyBar = MyBar[1] + 1;
			}
		}
	}
	ExtremeBar = MyBar;
	RETURN MyVal;
END


//------------------------------------------------------------------------
// 编译版本	GS2010.12.08
// 版权所有	TradeBlazer Software 2003－2010
// 更改声明	TradeBlazer Software保留对TradeBlazer平
//			台每一版本的TradeBlazer公式修改和重写的权利
//------------------------------------------------------------------------
