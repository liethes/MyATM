//------------------------------------------------------------------------
// 简称: DataConvert
// 名称: 跨周期数据转换函数
// 类别: 用户函数
// 类型: 内建函数
// 输出: 数值型
//------------------------------------------------------------------------

PARAMS
	NumericSeries Price(0); // 数据源
	String strPeriodType("min"); // 目标周期类型,Min,Day,Week,Month
	Numeric nPeriodNums(3); // 目标周期数目，和类型组合成 3Min,5Day,2Week,2Month等值
	String strDataType("close"); // 转换的类型，分为High,Low,Open,Close,Vol,OpenInt六种类型
VARS
	NumericSeries nNext(-1);
	NumericSeries returnValue;
	Numeric nCur;
	String strTmp1;
	String strTmp2;
BEGIN
	strTmp1 = Lower(strPeriodType);
	strTmp2 = Lower(strDataType);

	if (BarType == 1 && strTmp1 == "min" && BarInterval > nPeriodNums) // 当前数据周期是分钟线
		RETURN Price;

	if ((BarType == 0 || BarType == 4 || BarType == 5) && strTmp1 == "min")
		RETURN Price;

	if ((BarType == 4 || BarType == 5) && strTmp1 == "day")
		RETURN Price;

	if (BarType == 5 && strTmp1 == "week")
		RETURN Price;

	if (nPeriodNums <= 0) RETURN Price;

	if (strTmp2 != "open" && strTmp2 != "high" && strTmp2 != "low" &&
		strTmp2 != "close" && strTmp2 != "vol" && strTmp2 != "openint")
		RETURN Price;

	if (strTmp1 == "min") // 处理N分钟数据输出
	{
		nCur = (Hour * 60 + Minute) / nPeriodNums;
	}
	else if (strTmp1 == "day") // 处理N日数据输出
	{
		nCur = DateDiff(19700102, trueDate(0)) / nPeriodNums;
	}
	else if (strTmp1 == "week") // 处理N周数据输出
	{
		nCur = DateDiff(19700105, trueDate(0)) / (7 * nPeriodNums);
	}
	else if (strTmp1 == "month") // 处理N月数据输出
	{
		nCur = ((Year() - 1970) * 12 + Month) / nPeriodNums;
	}
	else {
		RETURN Price;
	}
	nCur = IntPart(nCur);

	if (BarStatus == 0) {
		nNext = nCur + 1;
		returnValue = Price;
	}
	else {
		if ((strTmp1 == "min" && trueDate(0) == trueDate(1) && nCur < nNext[1]) ||
			(strTmp1 != "min" && nCur < nNext[1])) {
			if (strTmp2 == "high") {
				if (Price > returnValue)
					returnValue = Price;
			}
			else if (strTmp2 == "low") {
				if (Price < returnValue)
					returnValue = Price;
			}
			else if (strTmp2 == "close" || strTmp2 == "openInt") {
				returnValue = Price;
			}
			else if (strTmp2 == "vol") {
				returnValue = returnValue + Price;
			}
		}
		else {
			nNext = nCur + 1;
			returnValue = Price;
		}
	}

	RETURN returnValue;
END

//------------------------------------------------------------------------
// 编译版本	GS2010.12.08
// 版权所有	TradeBlazer Software 2003－2010
// 更改声明	TradeBlazer Software保留对TradeBlazer平
//			台每一版本的TradeBlazer公式修改和重写的权利
//------------------------------------------------------------------------
