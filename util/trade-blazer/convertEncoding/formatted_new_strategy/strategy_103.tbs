//------------------------------------------------------------------------
// 简称: RSI
// 名称: 相对强弱指数
// 类别: 公式应用
// 类型: 内建应用
//------------------------------------------------------------------------

PARAMS
	Numeric Length(14);
	Numeric OverSold(30);
	Numeric OverBought(70);
VARS
	NumericSeries NetChgAvg(0);
	NumericSeries totChgAvg(0);
	Numeric SF(0);
	Numeric Change(0);
	Numeric ChgRatio(0);
	Numeric RSIValue;
BEGIN
	if (CurrentBar <= Length - 1) {
		NetChgAvg = (Close - Close[Length]) / Length;
		totChgAvg = Average(Abs(Close - Close[1]), Length);
	}
	else {
		SF = 1 / Length;
		Change = Close - Close[1];
		NetChgAvg = NetChgAvg[1] + SF * (Change - NetChgAvg[1]);
		totChgAvg = totChgAvg[1] + SF * (Abs(Change) - totChgAvg[1]);
	}

	if (totChgAvg < > 0) {
		ChgRatio = NetChgAvg / totChgAvg;
	}
	else {
		ChgRatio = 0;
	}
	RSIValue = 50 * (ChgRatio + 1);
	PlotNumeric("RSI", RSIValue);
	PlotNumeric("超买", OverBought);
	PlotNumeric("超卖", OverSold);
END

//------------------------------------------------------------------------
// 编译版本	GS2010.12.08
// 版权所有	TradeBlazer Software 2003－2010
// 更改声明	TradeBlazer Software保留对TradeBlazer平
//			台每一版本的TradeBlazer公式修改和重写的权利
//------------------------------------------------------------------------
