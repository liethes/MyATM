//------------------------------------------------------------------------
// 简称: CL_FourSetofMACrossoverSys_S
// 名称: 四均线交易系统 空
// 类别: 公式应用
// 类型: 内建应用
// 输出:
// 策略说明:
//				基于4均线系统进行判断交易
//
// 系统要素:
//				(5和20周期均线),(3和10周期均线)构成的两组不同周期的均线组合
//
// 入场条件:	
//				当2组均线均成空头排列时且当前价低于上根BAR最低价入场
//
// 出场条件:	
//				1 小周期空头均线组合成多头排列
//       	    2 两组多头均线分别多头排列且低于上根BAR最高价出场
//
//----------------------------------------------------------------------//
PARAMS
	Numeric LEFast(5); //多头入场短均线周期参数
	Numeric LESlow(20); //多头入场长均线周期参数 
	Numeric LXFast(3); //多头出场短均线周期参数
	Numeric LXSlow(10); //多头出场长均线周期参数
	Numeric SEFast(5); //空头入场短均线周期参数
	Numeric SESlow(20); //空头入场长均线周期参数
	Numeric SXFast(3); //空头出场短均线周期参数
	Numeric SXSlow(10); //空头出场长均线周期参数
VARS
	NumericSeries MALEFast; //多头入场短均线
	NumericSeries MALESlow; //多头入场长均线
	NumericSeries MALXFast; //多头出场短均线
	NumericSeries MALXSlow; //多头出场长均线
	NumericSeries MASEFast; //空头入场短均线
	NumericSeries MASESlow; //空头入场长均线
	NumericSeries MASXFast; //空头出场短均线
	NumericSeries MASXSlow; //空头出场长均线
BEGIN

// 集合竞价和小节休息过滤
	if (!CallAuctionFilter()) RETURN;

	MALEFast = Average(Close, LEFast); //多头入场短均线
	MALESlow = Average(Close, LESlow); //多头入场长均线
	MALXFast = Average(Close, LXFast); //多头出场短均线
	MALXSlow = Average(Close, LXSlow); //多头出场长均线
	MASEFast = Average(Close, SEFast); //空头入场短均线
	MASESlow = Average(Close, SESlow); //空头入场长均线
	MASXFast = Average(Close, SXFast); //空头出场短均线
	MASXSlow = Average(Close, SXSlow); //空头出场长均线

//系统入场
	if (Marketposition < > -1 and Currentbar >= 100) //两组均线均成空头排列时且当前价低于上根BAR最低价入场
	{
		if (MASEFast[1] < MASESlow[1] and MASXFast[1] < MASXSlow[1] and Low <= Low[1] And Vol > 0) {
			SellShort(0, Min(Open, Low[1]));
		}
	}

//系统出场
	if (MarketPosition == -1 and BarsSinceEntry > 0 And Vol > 0) {
		if (MASXFast[1] > MASXSlow[1]) //小周期空头均线组合成多头排列出场
		{
			BuytoCover(0, Open);
		}
		else if (MALEFast[1] > MALESlow[1] and MALXFast[1] > MALXSlow[1] and High >= High[1]) //两组均线分别多头排列且高于上根BAR最高价出场
		{
			BuytoCover(0, Max(Open, High[1]));
		}
	}
END

//------------------------------------------------------------------------
// 编译版本	GS2014.10.25
// 版权所有	TradeBlazer Software 2003－2014
// 更改声明	TradeBlazer Software保留对TradeBlazer平
//			台每一版本的TradeBlazer公式修改和重写的权利
//------------------------------------------------------------------------
