//------------------------------------------------------------------------
// 简称: TS_KingKeltner_S
// 名称: 金肯特纳_空
// 类别: 公式应用
// 类型: 内建应用
// 输出:
//------------------------------------------------------------------------

	/* 
	策略说明:
			基于肯特纳通道的突破系统
	系统要素:
			1、基于最高价、最低价、收盘价三者平均值计算而来的三价均线
			2、基于三价均线加减真实波幅计算而来的通道上下轨
	入场条件:
			1、三价均线向上，并且价格上破通道上轨，开多单
			2、三价均线向下，并且价格下破通道下轨，开空单
	出场条件:
			1、持有多单时，价格下破三价均线，平多单
			2、持有空单时，价格上破三价均线，平空单
	注    意:
			此公式仅做空
	*/
PARAMS
	Numeric avgLength(40); // 三价均线参数
	Numeric atrLength(40); // 真实波幅参数
	Numeric Lots(0); // 交易手数
VARS
	NumericSeries movAvgVal(0); // 三价均线参数
	NumericSeries dnBand(0); // 通道下轨
	NumericSeries liquidPoint(0); // 出场条件
BEGIN
// 集合竞价和小节休息过滤
	if (!CallAuctionFilter()) RETURN;

// 三价均线
	movAvgVal = Average((High + Low + Close) / 3, avgLength);
// 通道下轨
	dnBand = movAvgVal - AvgtrueRange(atrLength);
// 出场条件
	liquidPoint = movAvgVal;

// 画线
	PlotNumeric("movAvgVal", movAvgVal);
	PlotNumeric("dnBand", dnBand);

// 三价均线向下，并且价格下破通道下轨，开空单
	if (MarketPosition != -1 And movAvgVal[1] < movAvgVal[2] And Low <= dnBand[1]) SellShort(Lots, Min(Open, dnBand[1]));
// 持有空单时，价格上破三价均线，平空单
	if (MarketPosition == -1 And BarsSinceEntry >= 1 And High >= liquidPoint[1]) BuytoCover(0, Max(Open, liquidPoint[1]));
END

//------------------------------------------------------------------------
// 编译版本	GS2014.10.25
// 版权所有	TradeBlazer Software 2003－2014
// 更改声明	TradeBlazer Software保留对TradeBlazer平
//			台每一版本的TradeBlazer公式修改和重写的权利
//------------------------------------------------------------------------
