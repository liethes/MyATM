//------------------------------------------------------------------------
// 简称: Kurtosis
// 名称: 求峰度系数
// 类别: 用户函数
// 类型: 内建函数
// 输出: 数值型
//------------------------------------------------------------------------

Params
	NumericSeries Price(1); 
	Numeric Length(10);
Vars
	Numeric KurtValue(0);
	Numeric P1(0);
	Numeric P2(0);
	Numeric P3(0);
	Numeric Mean;
	Numeric SDev;
	Numeric i;
Begin
	If (Length >3)
	{
		Mean = Average( Price, Length ) ;
		SDev = StandardDev( Price, Length, 2 ) ;
		if (SDev > 0)
		{
			for  i = 0 to Length - 1
			{
				P2 = P2 + Power( ( Price[i] - Mean ) / SDev, 4 ) ;
			}
			P1 = Length * ( Length + 1 ) / ( ( Length - 1 ) * ( Length - 2 ) * ( Length - 3 ) ) ;
			P3 = 3 * Sqr( Length - 1 ) / ( ( Length - 2 ) * ( Length - 3 ) ) ;
			KurtValue = P1 * P2 - P3 ;
		}
	}
	Return KurtValue;
End

//------------------------------------------------------------------------
// 编译版本	GS2010.12.08
// 版权所有	TradeBlazer Software 2003－2010
// 更改声明	TradeBlazer Software保留对TradeBlazer平
//			台每一版本的TradeBlazer公式修改和重写的权利
//------------------------------------------------------------------------