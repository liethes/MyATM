//------------------------------------------------------------------------
// 简称: LinearReg
// 名称: 求线性回归
// 类别: 用户函数
// 类型: 内建函数
// 输出: 布尔型
//------------------------------------------------------------------------

Params
	NumericSeries Price(1);
	Numeric Length(10);
	Numeric TgtBar(0);
	NumericRef LRSlope; 
	NumericRef LRAngle;
	NumericRef LRIntercept;
	NumericRef LRValue;
Vars
	Numeric SumXY(0);
	Numeric SumY;
	Numeric SumX; 
	Numeric SumXSqr;
	Numeric Divisor;
	Numeric i;
Begin
	if (Length > 1)
	{
		SumX = Length * ( Length - 1 ) * 1/2;
		SumXSqr = Length * ( Length - 1 ) * ( 2 * Length - 1 ) * 1/6 ;	
		Divisor = Sqr( SumX ) - Length * SumXSqr ;			
		SumY = Summation( Price, Length ) ;
		for i = 0 to Length - 1 
		{
			SumXY = SumXY + i * Price[i] ;
		}
		
		LRSlope = ( Length * SumXY - SumX * SumY) / Divisor ;
		LRAngle = Atan ( LRSlope ) ;
		LRIntercept = ( SumY - LRSlope * SumX ) / Length ;
		LRValue = LRIntercept + (Length - 1 - TgtBar)*LRSlope;
		Return True;
	}Else
	{
		Return False;
	}
End

//------------------------------------------------------------------------
// 编译版本	GS2010.12.08
// 版权所有	TradeBlazer Software 2003－2010
// 更改声明	TradeBlazer Software保留对TradeBlazer平
//			台每一版本的TradeBlazer公式修改和重写的权利
//------------------------------------------------------------------------